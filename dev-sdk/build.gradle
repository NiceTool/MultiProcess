apply plugin: 'com.android.library'

//****************buildSrc依赖选项*****************
apply plugin: 'costtime'
costtime {
    enable true
    costAll false
    timeout 10
    enableLog true
}
//****************打包时配置参数*****************
// 打正式包,正式包开关: bugly关闭，日志关闭，内网地址，严格模式
def isRelease = true
//SDK版本号
def ver = "4.3.0.7"
def date = "20200201"
//SDK子版本
def subVersion = "00"
//当前热修复包的版本,默认是1,需要递增
def hf_code = 1
// 宿主包(jar):true , 热修复包(dex):false
def isPackageJar = true


// 上传时间，几小时
def default_request_hours = 6
// in HTTP Header use Kx, when debug
def isNativeDebug = true

//********************子项目控制，只有在isReleases=false时才生效*********************


//编译版本内部使用
def version = "${ver}|${date}"
//是否启用严格模式。  true:检测，false: 不检测
def BUILD_USE_STRICTMODE = true
// 包含热修复功能:true ， 不包含热修复功能:false
def enableHotFix = true
def ENABLE_BUGLY = true
def DEBUG_URL = true
//所有的log开关,true 打印所有的日志,false,关闭所有的日志
def logcat = true
// 内部加密字符串试用的密钥，StringFog_G生成时用，
// 在StringFogTransform.groovy之前生成加密串放置于com.analysys.plugin.StringFog bs
def STRING_FOG_KEY = "4.3.0.7|20200201"

//********************日志tag相关*********************
//region 内部日志相关
def tag_hotfix = "analysys_hotfix" //热修复 tag
def tag_snap = "analysys_snap"//安装列表tag
def tag_upload = "analysys_upload"//上传的tag
def tag_oc = "analysys_oc"//oc的tag
def tag_usm = "analysys_usm"//usm的tag
def tag_netinfo = "analysys_netinfo"//网络扫描的tag
def tag_loc = "analysys_loc"//位置信息的tag
def tag_recerver = "analysys_recerver"//广播接收的tag
def tag_cutoff = "analysys_cutoff"//短路的tag
def tag_stringfog = "analysys_stringfog"//字符串混淆的tag
//endregion

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'

    defaultConfig {
        def sdkv = "${version}"
        if (enableHotFix) {
            sdkv += "${subVersion}"
        }
        // 勿动，调试修改isRelease为false即可
        if (isRelease) {
            ENABLE_BUGLY = false
            DEBUG_URL = false
            logcat = false
            BUILD_USE_STRICTMODE = true
        }

        buildConfigField "int", "DEF_REQ_HOUR", "${default_request_hours}"

        buildConfigField "String", "SDK_VERSION", "\"${sdkv}\""
        buildConfigField "String", "STRING_FOG_KEY", "\"${STRING_FOG_KEY}\""
        buildConfigField "boolean", "IS_HOST", "${isPackageJar}"
        buildConfigField "boolean", "enableHotFix", "${enableHotFix}"

        buildConfigField "String", "hf_code", isPackageJar ? "\"0\"" : "\"${hf_code}\""
        buildConfigField "boolean", "ENABLE_BUGLY", "${ENABLE_BUGLY}"
        buildConfigField "boolean", "BUILD_USE_STRICTMODE", "${BUILD_USE_STRICTMODE}"
        buildConfigField "boolean", "DEBUG_URL", "${DEBUG_URL}"

        buildConfigField "boolean", "logcat", "${logcat}"
        buildConfigField "boolean", "isNativeDebug", "${isNativeDebug}"
        buildConfigField "String", "tag_hotfix", "\"${tag_hotfix}\""
        buildConfigField "String", "tag_snap", "\"${tag_snap}\""
        buildConfigField "String", "tag_upload", "\"${tag_upload}\""
        buildConfigField "String", "tag_oc", "\"${tag_oc}\""
        buildConfigField "String", "tag_usm", "\"${tag_usm}\""
        buildConfigField "String", "tag_netinfo", "\"${tag_netinfo}\""
        buildConfigField "String", "tag_loc", "\"${tag_loc}\""
        buildConfigField "String", "tag_recerver", "\"${tag_recerver}\""
        buildConfigField "String", "tag_cutoff", "\"${tag_cutoff}\""
        buildConfigField "String", "tag_stringfog", "\"${tag_stringfog}\""


        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            /**打开覆盖率统计开关*/
            //testCoverageEnabled = true
            minifyEnabled false
            if (isPackageJar) {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-jar.pro'
            } else {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dex.pro'
            }
        }
        release {
            minifyEnabled true
            if (isPackageJar) {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-jar.pro'
            } else {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-dex.pro'
            }
        }
    }

    dexOptions {
        preDexLibraries false
        maxProcessCount 8
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        warningsAsErrors false
        disable "UnusedResources"
        textOutput "stdout"
        textReport false
        disable 'RestrictedApi'
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'org.robolectric:robolectric:3.0'
    compileOnly files("libs/miit_mdid_1.0.10.aar")
}

//########################################################################
//#####################################task ###########################
//########################################################################

// zip dir
def zipPathDir = "../release/${ver}_${date}/"
def name = "analysys_track_v${ver}_${date}" + (enableHotFix ? "${subVersion}" : "")
def defaultJar = "classes.jar"

task generateJar(type: Copy) {
    from("build/intermediates/bundles/release")
    into "../releasebak"
    include("${defaultJar}")
    rename("${defaultJar}", "${name}.jar")
}

task copyDoc(type: Copy) {
    from("../doc")
    into "../releasebak"
    include("*.pdf")
}

//https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html
task packageZip(type: Zip) {
    from "../releasebak/"
//    destinationDir: 来指定生成zip文件的目录，类型是file(dir,但是不要包含/)
    destinationDir = file(zipPathDir)
//    archiveName: 压缩包名字，注意此处不会自动添加后缀
//    archiveName = "analysys_track_v" + version.replace("|", "_") + (enableHotFix ? "00.zip" : ".zip")
    archiveName = "${name}.zip"
}

task copyJar(type: Copy) {
    from("build/intermediates/bundles/release")
    into "${zipPathDir}"
    include("${defaultJar}")
}

def isWindows() {
    return org.gradle.internal.os.OperatingSystem.current().isWindows()
    //return org.gradle.internal.os.OperatingSystem.current().isWindows()
    //return System.properties['os.name'].contains('windows');
}
// 执行shell命令.如果是打dex 包，则
task runDx(type: Exec) {
//    commandLine 'pwd'
//    commandLine '../dx.sh'
//    commandLine "dx", "--dex", "--output" ,"001.dex","../release/4.3.0.7_20200201/classes.jar"

    if (isWindows()) {
        commandLine "cmd", "/c", "dx", "--dex", "--output", "${zipPathDir}${name}.dex", "${zipPathDir}${defaultJar}"
    } else {
        commandLine "dx", "--dex", "--output", "${zipPathDir}${name}.dex", "${zipPathDir}${defaultJar}"
    }


}

task bakMappiing(type: Copy) {
    from("build/outputs/mapping/release")
    into "${zipPathDir}"
    include("mapping.txt")
    rename("mapping.txt", "${ver}_${date}${subVersion}-mapping.txt")
}
task zip(type: Delete) {
    delete "../releasebak"
    delete "${zipPathDir}${defaultJar}"
}

if (isPackageJar) {
    generateJar.dependsOn assemble
    copyDoc.dependsOn generateJar
    packageZip.dependsOn copyDoc
    bakMappiing.dependsOn packageZip
    zip.dependsOn bakMappiing
} else {
    copyJar.dependsOn assemble
    runDx.dependsOn copyJar
    bakMappiing.dependsOn runDx
    zip.dependsOn bakMappiing
}
